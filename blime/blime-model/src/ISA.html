
<!DOCTYPE html>

<html lang="en">
  <head>
    <meta charset="utf-8" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0" /><meta name="generator" content="Docutils 0.19: https://docutils.sourceforge.io/" />

    <title>Model ISA &#8212; Blinded Memory  documentation</title>
    <link rel="stylesheet" type="text/css" href="../../_static/pygments.css" />
    <link rel="stylesheet" type="text/css" href="../../_static/alabaster.css" />
    <link rel="stylesheet" type="text/css" href="https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.36.0/codemirror.min.css" />
    <link rel="stylesheet" type="text/css" href="../../_static/cm.tango.css" />
    <link rel="stylesheet" type="text/css" href="../../_static/fslit.css" />
    <script data-url_root="../../" id="documentation_options" src="../../_static/documentation_options.js"></script>
    <script src="../../_static/doctools.js"></script>
    <script src="../../_static/sphinx_highlight.js"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.36.0/codemirror.min.js"></script>
    <script src="../../_static/fstar.cm.js"></script>
    <script src="../../_static/fslit.js"></script>
    <script async="async" src="https://cdn.jsdelivr.net/npm/mathjax@3/es5/tex-mml-chtml.js"></script>
    <link rel="index" title="Index" href="../../genindex.html" />
    <link rel="search" title="Search" href="../../search.html" />
    <link rel="next" title="Blindable values" href="Value.html" />
    <link rel="prev" title="Load-store model" href="InstructionDecoder.html" />
   
  <link rel="stylesheet" href="../../_static/custom.css" type="text/css" />
  
  
  <meta name="viewport" content="width=device-width, initial-scale=0.9, maximum-scale=0.9" />

  </head><body>
  

    <div class="document">
      <div class="documentwrapper">
        <div class="bodywrapper">
          

          <div class="body" role="main">
            
  <section id="model-isa">
<h1>Model ISA<a class="headerlink" href="#model-isa" title="Permalink to this heading">¶</a></h1>
<p>In this file, we analyze a concrete instruction set that includes a number
of manually-implemented taint propagation rules.</p>
<pre class="code fstar literal-block"><code>module ISA

open FStar.Math.Lib
open FStar.Mul

open Cpu
open Memory
open InstructionDecoder
open Value
open Multi</code></pre>
<section id="instruction-format">
<h2>Instruction format<a class="headerlink" href="#instruction-format" title="Permalink to this heading">¶</a></h2>
<p>We start by defining the instruction format.</p>
<table class="docutils align-default">
<tbody>
<tr class="row-odd"><td><p><strong>Field</strong></p></td>
<td colspan="2"><p>opcode</p></td>
<td colspan="2"><p>out1</p></td>
<td colspan="2"><p>in1</p></td>
<td colspan="2"><p>in2</p></td>
<td colspan="2"><p>imm</p></td>
</tr>
<tr class="row-even"><td><p><strong>Bits</strong></p></td>
<td><p>63</p></td>
<td><p>61</p></td>
<td><p>60</p></td>
<td><p>56</p></td>
<td><p>55</p></td>
<td><p>51</p></td>
<td><p>50</p></td>
<td><p>46</p></td>
<td><p>45</p></td>
<td><p>0</p></td>
</tr>
</tbody>
</table>
<p>Given this format, we then define functions to extract each component of the
instruction word.</p>
<p>First, we define a conversion function mapping a machine word to an integer.</p>
<pre class="code fstar literal-block"><code>let v = FStar.UInt64.v</code></pre>
<p>Then we define functions that extract the various components</p>
<pre class="code fstar literal-block"><code>let parse_opcode (inst:word) = (arithmetic_shift_right (v inst) 61) % 8
let parse_out1 (inst:word): nat = (arithmetic_shift_right (v inst) 56) % 32
let parse_in1 (inst:word): nat = (arithmetic_shift_right (v inst) 51) % 32
let parse_in2 (inst:word): nat = (arithmetic_shift_right (v inst) 45) % 32</code></pre>
<p>However, for immediate values it is more convenient to define the function
to extract a machine word rather than an integer.</p>
<pre class="code fstar literal-block"><code>let parse_imm (inst:word): Cpu.word =
      (FStar.UInt64.logand inst (FStar.UInt64.sub (FStar.UInt64.shift_left 1uL 46ul) 1uL))</code></pre>
</section>
<section id="instruction-decoder">
<h2>Instruction decoder<a class="headerlink" href="#instruction-decoder" title="Permalink to this heading">¶</a></h2>
<p>Next we set up the instruction decoder, which converts an instruction word
into an opcode plus a set of operands.</p>
<pre class="code fstar literal-block"><code>let sample_decoder (inst:word): decodedInstruction =
  let opcode = parse_opcode inst in
  let out1 = parse_out1 inst in
  let in1 = parse_in1 inst in
  let in2 = parse_in2 inst in
  let imm = parse_imm inst in
  match opcode with
  (* Store *)
    | 0 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2 ];
            output_operands = [] }
  (* Load *)
    | 1 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2 ];
            output_operands = [] }
  (* Branch *)
    | 2 -&gt; { opcode;
            input_operands  = [PC; Register in1; Register in2];
            output_operands = [PC] }
  (* Add *)
    | 3 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2; Immediate imm ];
            output_operands = [ Register out1 ] }
  (* Subtract *)
    | 4 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2 ];
            output_operands = [ Register out1 ] }
  (* Multiply *)
    | 5 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2 ];
            output_operands = [ Register out1 ] }
  (* Bitwise AND *)
    | 6 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2 ];
            output_operands = [ Register out1 ] }
  (* Bitwise XOR *)
    | 7 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2 ];
            output_operands = [ Register out1 ] }</code></pre>
<p>Later on we will need to know how many input operands each instruction has;
the prover can work it out, but needs a hint.</p>
<pre class="code fstar literal-block"><code>let sample_decoded_instruction_length (inst:word):
  Lemma (ensures FStar.List.length (sample_decoder inst).input_operands =
                    (match (sample_decoder inst).opcode with
                     | 0 -&gt; 2
                     | 1 -&gt; 2
                     | 2 -&gt; 3
                     | 3 -&gt; 3
                     | 4 -&gt; 2
                     | 5 -&gt; 2
                     | 6 -&gt; 2
                     | 7 -&gt; 2
                     )
        )
        [SMTPat (sample_decoder inst)] =
    match (sample_decoder inst).opcode with
        | 0 -&gt; ()
        | 1 -&gt; ()
        | 2 -&gt; ()
        | 3 -&gt; ()
        | 4 -&gt; ()
        | 5 -&gt; ()
        | 6 -&gt; ()
        | 7 -&gt; ()
</code></pre>
<section id="utility-functions">
<h3>Utility functions<a class="headerlink" href="#utility-functions" title="Permalink to this heading">¶</a></h3>
<pre class="code fstar literal-block"><code>type blindedness_state =
  | NoneBlinded : blindedness_state
  | ConsistentlyBlinded : blindedness_domain -&gt; blindedness_state
  | InconsistentlyBlinded : blindedness_state

let rec blindedness_state_of_list_int (l : list blindedWord) (s : blindedness_state) : blindedness_state =
  if InconsistentlyBlinded? s then s else
  match l with
    | Nil -&gt; s
    | MultiClear x :: tl -&gt; blindedness_state_of_list_int tl s
    | MultiBlinded x d :: tl -&gt; let s2 = match s with
                                          | NoneBlinded -&gt; ConsistentlyBlinded d
                                          | ConsistentlyBlinded d2 -&gt; if d = d2 then s else InconsistentlyBlinded
                                          | InconsistentlyBlinded -&gt; InconsistentlyBlinded
                                  in
                                blindedness_state_of_list_int tl s2

let blindedness_state_of_list (l : list blindedWord) : blindedness_state =
  blindedness_state_of_list_int l NoneBlinded

let rec blind_all_values (values: list blindedWord) (d: blindedness_domain): r:(list blindedWord){FStar.List.length values = FStar.List.length r} =
    match values with
      | Nil                     -&gt; Nil
      | MultiBlinded hd dx :: tl -&gt; MultiBlinded hd dx :: blind_all_values tl d
      | MultiClear   hd    :: tl -&gt; MultiBlinded hd d :: blind_all_values tl d

let rec blinding_all_values_blinds_each_value (values: list blindedWord) (d: blindedness_domain) (n:nat{n &lt; FStar.List.length values}):
  Lemma (ensures is_blinded (FStar.List.Tot.index (blind_all_values values d) n))
        [SMTPat (is_blinded (FStar.List.Tot.index (blind_all_values values d) n))] =
  match n, values with
    | 0, _   -&gt; ()
    | _, Nil -&gt; ()
    | n, hd :: tl -&gt; blinding_all_values_blinds_each_value tl d (n-1)


let rec blinding_all_values_is_idempotent  (values: list blindedWord) (d: blindedness_domain):
  Lemma (ensures (blind_all_values values d) = blind_all_values (blind_all_values values d) d) =
    match values with
      | hd :: tl -&gt; blinding_all_values_is_idempotent tl d
      | _ -&gt; ()

let rec blinding_all_values_blinds_all_values  (values: list blindedWord) (d: blindedness_domain):
  Lemma (ensures all_values_are_blinded (blind_all_values values d))
        [SMTPat (blind_all_values values d)] =
  match values with
    | hd :: tl -&gt; blinding_all_values_blinds_all_values tl d
    | _ -&gt; ()

let rec equivalent_unblinded_lists_are_equal (a b: list blindedWord):
  Lemma (requires (equiv_list a b) /\ (~(any_value_is_blinded a) \/ ~(any_value_is_blinded b)))
        (ensures a = b) =
  match a, b with
    | h1 :: t1, h2 :: t2 -&gt; equivalent_unblinded_lists_are_equal t1 t2
    | _ -&gt; ()
</code></pre>
</section>
</section>
<section id="instruction-semantics">
<h2>Instruction semantics<a class="headerlink" href="#instruction-semantics" title="Permalink to this heading">¶</a></h2>
<p>Now we define the behavior of each instruction.</p>
<p>In most cases we use a ‘generic’ taint propagation rule:</p>
<div class="exercise docutils container">
<span class="exercise-header"><span class="exercise-label">Exercise</span></span><div class="exercise-body docutils container">
<p>if any_value_is_blinded pre then Blinded result else Clear result</p>
</div>
</div>
<p>However, there are some instructions where a more specific rule is
appropriate.  For example, <span class="math notranslate nohighlight">\(x \oplus x\)</span> and <span class="math notranslate nohighlight">\(x \&amp; 0\)</span> both
evaluate to zero, irrespective of the value of <span class="math notranslate nohighlight">\(x\)</span>, meaning that
the result can be <code class="docutils literal notranslate"><span class="pre">Clear</span> <span class="pre">0</span></code> even though an input may be blinded.</p>
<pre class="code fstar literal-block"><code>val sample_semantics (#n:memory_size): instruction_semantics #n #32 sample_decoder

#set-options &quot;--z3rlimit 1000&quot;
let sample_semantics (di:decoder_output sample_decoder)
                     (pre:(list blindedWord){
                       (exists(s: systemState). pre = get_operands di.input_operands s) /\
                       FStar.List.length pre = FStar.List.length di.input_operands})
                     : instruction_result di =
  match di.opcode with
    (* Store *)
    | 0 -&gt; let address = (FStar.List.Tot.index pre 0) in

    { register_writes = []; memory_ops = (
      assert(FStar.List.length di.input_operands = 2);
      match (FStar.List.Tot.index di.input_operands 0), (FStar.List.Tot.index di.input_operands 1) with
        | Register d, Register s -&gt; [Store (v (unwrap address)) s]
        | _ -&gt; []
      ); fault = is_blinded address}
    (* Load *)
    | 1 -&gt; let address = (FStar.List.Tot.index pre 0) in
          { register_writes = [];
            memory_ops = (match (FStar.List.Tot.index di.input_operands 0),
                                     (FStar.List.Tot.index di.input_operands 1) with
                                | Register d, Register s -&gt; [Load (v (unwrap address)) d]
                                | _ -&gt; []);
            fault = is_blinded address}
    (* Branch *)
    | 2 -&gt; ( let pc = FStar.List.Tot.index pre 0 in
            let a = FStar.List.Tot.index pre 1 in
            let b = FStar.List.Tot.index pre 2 in
            let ref: blindedWord = MultiClear #word 0uL in
            { register_writes = [if a = ref then b else pc];
              memory_ops = [];
              fault = is_blinded a})
    (* Add *)
    | 3 -&gt; ( assert(FStar.List.length pre = 3);
            let a = FStar.List.Tot.index pre 0 in
            let b = FStar.List.Tot.index pre 1 in
            let result: Cpu.word = (FStar.UInt64.add_mod (unwrap a) (unwrap b)) in
            let result = match blindedness_state_of_list pre with
                          | NoneBlinded -&gt; make_clear result
                          | ConsistentlyBlinded d -&gt; MultiBlinded result d
                          | InconsistentlyBlinded -&gt; make_clear 0uL in
            { register_writes = [result];
              memory_ops = [];
              fault = InconsistentlyBlinded? (blindedness_state_of_list pre)})
    (* Sub *)
    | 4 -&gt; ( assert(FStar.List.length pre = 2);
            let a = FStar.List.Tot.index pre 0 in
            let b = FStar.List.Tot.index pre 1 in
            let result: Cpu.word = (FStar.UInt64.sub_mod (unwrap a) (unwrap b)) in
            let result = match blindedness_state_of_list pre with
                          | NoneBlinded -&gt; make_clear result
                          | ConsistentlyBlinded d -&gt; MultiBlinded result d
                          | InconsistentlyBlinded -&gt; make_clear 0uL in
            { register_writes = [result];
              memory_ops = [];
              fault = InconsistentlyBlinded? (blindedness_state_of_list pre) })
    (* MUL *)
    | 5 -&gt; ( assert(FStar.List.length pre = 2);
            let a = FStar.List.Tot.index pre 0 in
            let b = FStar.List.Tot.index pre 1 in
            let result = (FStar.UInt64.mul_mod (unwrap a) (unwrap b)) in
            let result = match blindedness_state_of_list pre with
                          | NoneBlinded -&gt; make_clear result
                          | ConsistentlyBlinded d -&gt; MultiBlinded result d
                          | InconsistentlyBlinded -&gt; make_clear 0uL in
           { register_writes = [result];
             memory_ops = [];
              fault = InconsistentlyBlinded? (blindedness_state_of_list pre) })
    (* AND *)
    | 6 -&gt; ( assert(FStar.List.length pre = 2);
            let a = FStar.List.Tot.index pre 0 in
            let b = FStar.List.Tot.index pre 1 in
            let result: Cpu.word = FStar.UInt64.logand (unwrap a) (unwrap b) in
            let result = if (a = make_clear 0uL) || (b = make_clear 0uL) then
                            make_clear 0uL
                         else match blindedness_state_of_list pre with
                          | NoneBlinded -&gt; make_clear result
                          | ConsistentlyBlinded d -&gt; MultiBlinded result d
                          | InconsistentlyBlinded -&gt; make_clear 0uL in
                         { register_writes = [result];
                           memory_ops = [];
                           fault = InconsistentlyBlinded? (blindedness_state_of_list pre)}
          )
    (* XOR *)
    | 7 -&gt; ( assert(FStar.List.length pre = 2);
            let a = FStar.List.Tot.index pre 0 in
            let b = FStar.List.Tot.index pre 1 in
            let result: Cpu.word = (FStar.UInt64.logxor (unwrap a) (unwrap b)) in
            let result = if (FStar.List.Tot.index di.input_operands 0)
                           = (FStar.List.Tot.index di.input_operands 1) then
                            make_clear 0uL
                         else match blindedness_state_of_list pre with
                          | NoneBlinded -&gt; make_clear result
                          | ConsistentlyBlinded d -&gt; MultiBlinded result d
                          | InconsistentlyBlinded -&gt; make_clear 0uL in
                         { register_writes = [result];
                           memory_ops = [];
                           fault = InconsistentlyBlinded? (blindedness_state_of_list pre)}
          )

irreducible let trigger (#n: memory_size)
  (inst:word) (pre:instruction_input #n #32 (sample_decoder inst)) = False</code></pre>
<section id="safety">
<h3>Safety<a class="headerlink" href="#safety" title="Permalink to this heading">¶</a></h3>
<p>Now we can start to prove the safety of the instruction semantics, which we
do by proving their redaction-equivalence.</p>
<p>This is complicated slightly by the fact that we need to show that the
redacted inputs are also valid inputs.</p>
<p>We start by using an special redacting function on the instruction inputs
that shows that the result is also an input to said instruction.</p>
<pre class="code fstar literal-block"><code>#set-options &quot;--z3rlimit 1000&quot;
let sample_semantics_are_redacting_equivalent_expanded (#n: memory_size)
   (inst:word)
   (pre:instruction_input (sample_decoder inst)):
  Lemma (ensures (equiv_result (sample_semantics #n (sample_decoder inst) pre)
                               (sample_semantics #n (sample_decoder inst)
                                  (redacted_instruction_inputs_are_instruction_inputs #n #32
                                     (sample_decoder inst) pre)))
                 \/ trigger #n inst pre) =
  ()</code></pre>
<p>Then we do the real proof of redacting-equivalence.</p>
<pre class="code fstar literal-block"><code>#set-options &quot;--z3rlimit 10000&quot;
let sample_semantics_are_redacting_equivalent (#n: memory_size)
    (inst:word)
    (pre:instruction_input #n #32 (sample_decoder inst)):
  Lemma (ensures is_redacting_equivalent_instruction_semantics_somewhere #n #32
                                                                         sample_decoder
                                                                         sample_semantics
                                                                         inst pre
                 \/ trigger inst pre) =
  ()

let sample_semantics_are_redacting_equivalent_everywhere (#n: memory_size):
  Lemma (ensures is_redacting_equivalent_instruction_semantics_everywhere #n #32
                   sample_decoder sample_semantics) =
  ()</code></pre>
<p>Finally, we show that the semantics are safe.</p>
<pre class="code fstar literal-block"><code>let sample_semantics_are_safe (#n: memory_size) (cp:cache_policy n):
  Lemma (ensures is_safe #n #32
                         (loadstore_execution_unit #n #32 sample_decoder (sample_semantics #n) cp)) =
    sample_semantics_are_redacting_equivalent_everywhere #n;

    loadstore_execution_unit_with_re_instruction_semantics_is_redacting_equivalent
      sample_decoder (sample_semantics #n) cp;

    each_loadstore_execution_unit_with_redacting_equivalent_instruction_semantics_is_safe
      sample_decoder (sample_semantics #n) cp
</code></pre>
</section>
<section id="correctness">
<h3>Correctness<a class="headerlink" href="#correctness" title="Permalink to this heading">¶</a></h3>
<p>Next we prove that the instructions do the right thing.  First, addition:</p>
<pre class="code fstar literal-block"><code>let add_instruction_works_correctly
  (#n:memory_size) (inst:word)
  (pre:(list blindedWord){
    (exists(s: systemState #n).
      pre = get_operands #n #32 (sample_decoder inst).input_operands s) /\
      FStar.List.length pre = FStar.List.length (sample_decoder inst).input_operands}):

    Lemma (requires parse_opcode inst = 3 /\ ~(InconsistentlyBlinded? (blindedness_state_of_list pre)))
          (ensures  unwrap (FStar.List.Tot.hd
                             (sample_semantics #n (sample_decoder inst) pre).register_writes)
                     = (FStar.UInt64.add_mod (unwrap (FStar.List.Tot.index pre 0))
                                             (unwrap (FStar.List.Tot.index pre 1)))) =
    ()
</code></pre>
<p>Then exclusive or.  First we need to know that <span class="math notranslate nohighlight">\(x \oplus x = 0\)</span>, since this means
that the output won’t depend on the specific value of <span class="math notranslate nohighlight">\(x\)</span>.</p>
<pre class="code fstar literal-block"><code>let xor_with_self_yields_zero (a:Cpu.word):
  Lemma (ensures (FStar.UInt64.logxor a a) = 0uL) =
    let value: FStar.UInt.uint_t 64 = v a in
    FStar.UInt.logxor_self #64 value;
    assert(FStar.UInt.logxor #64 value value == 0)</code></pre>
<p>Then we show that the computed value really is the exclusive OR of the inputs.</p>
<pre class="code fstar literal-block"><code>let xor_instruction_works_correctly
      (#n:memory_size) (inst:word)
      (pre:(list blindedWord){
        (exists(s: systemState #n).
          pre = get_operands #n #32 (sample_decoder inst).input_operands s) /\
          FStar.List.length pre = FStar.List.length (sample_decoder inst).input_operands}):
    Lemma (requires parse_opcode inst = 7  /\ ~(InconsistentlyBlinded? (blindedness_state_of_list pre)))
          (ensures  unwrap (FStar.List.Tot.hd
                             (sample_semantics #n (sample_decoder inst) pre).register_writes)
                     = FStar.UInt64.logxor (unwrap (FStar.List.Tot.index pre 0))
                                           (unwrap (FStar.List.Tot.index pre 1)) ) =
          let di = sample_decoder inst in
          let a = FStar.List.Tot.index pre 0 in
          let b = FStar.List.Tot.index pre 1 in
          let outputs = (sample_semantics #n (sample_decoder inst) pre).register_writes in
          if (FStar.List.Tot.index di.input_operands 0)
              = (FStar.List.Tot.index di.input_operands 1) then (
            assert(a == b);
            xor_with_self_yields_zero (unwrap a) )
          else (
            let bs = blindedness_state_of_list pre in
            match bs with
                | ConsistentlyBlinded d -&gt; assert(outputs = [MultiBlinded (FStar.UInt64.logxor (unwrap a) (unwrap b)) d])
                | NoneBlinded -&gt; assert(outputs = [MultiClear (FStar.UInt64.logxor (unwrap a) (unwrap b))])
          )</code></pre>
</section>
</section>
</section>


          </div>
          
        </div>
      </div>
      <div class="sphinxsidebar" role="navigation" aria-label="main navigation">
        <div class="sphinxsidebarwrapper">
<h1 class="logo"><a href="../../index.html">Blinded Memory</a></h1>








<h3>Navigation</h3>
<ul class="current">
<li class="toctree-l1"><a class="reference external" href="https://arxiv.org/abs/2204.09649">Preprint</a></li>
<li class="toctree-l1"><a class="reference external" href="https://github.com/ssg-research/blime">Source code</a></li>
<li class="toctree-l1 current"><a class="reference internal" href="../index.html">Formal model</a></li>
</ul>

<div class="relations">
<h3>Related Topics</h3>
<ul>
  <li><a href="../../index.html">Documentation overview</a><ul>
  <li><a href="../index.html">Formal model of BliME</a><ul>
      <li>Previous: <a href="InstructionDecoder.html" title="previous chapter">Load-store model</a></li>
      <li>Next: <a href="Value.html" title="next chapter">Blindable values</a></li>
  </ul></li>
  </ul></li>
</ul>
</div>
<div id="searchbox" style="display: none" role="search">
  <h3 id="searchlabel">Quick search</h3>
    <div class="searchformwrapper">
    <form class="search" action="../../search.html" method="get">
      <input type="text" name="q" aria-labelledby="searchlabel" autocomplete="off" autocorrect="off" autocapitalize="off" spellcheck="false"/>
      <input type="submit" value="Go" />
    </form>
    </div>
</div>
<script>document.getElementById('searchbox').style.display = "block"</script>








        </div>
      </div>
      <div class="clearer"></div>
    </div>
    <div class="footer">
      &copy;2022&ndash;2023 Secure Systems Group.
      
      |
      Powered by <a href="http://sphinx-doc.org/">Sphinx 6.2.1</a>
      &amp; <a href="https://github.com/bitprophet/alabaster">Alabaster 0.7.13</a>
      
      |
      <a href="../../_sources/blime-model/src/ISA.fst.txt"
          rel="nofollow">Page source</a>
    </div>

    

    
  </body>
</html>